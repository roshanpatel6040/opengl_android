apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// Downloaded arcore .so files directory
def arcore_libpath = "${buildDir}/arcore-native"

// Create new configuration
configurations { natives }

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.demo.opengl"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_ARM_NEON=true', '-DANDROID_PLATFORM=android-24', '-DANDROID_TOOLCHAIN=clang',
                        "-DARCORE_LIBPATH=${arcore_libpath}/jni"
                abiFilters 'x86', 'x86_64', 'arm64-v8a', 'armeabi-v7a'
                cppFlags "-std=c++17", "-fexceptions", "-frtti"
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.firebase:firebase-ml-vision:24.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation 'com.google.android.material:material:1.2.1'

    implementation 'com.google.mlkit:face-detection:16.0.3'
    implementation 'com.google.ar:core:1.30.0'
    // new configuration to copy arcore .so into build folder
    // Uncomment to re-download so
    // Uncomment if arcore version is upgraded here
    // natives 'com.google.ar:core:1.30.0'
}


/**
 * Copy .so files from arcore into build folder
 * */
task extractNativeLibraries() {
    // Always extract, this insures the native libs are updated if the version changes.
    outputs.upToDateWhen { false }
    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into arcore_libpath
                include "jni/**/*"
            }
        }
    }
}

tasks.whenTaskAdded {
    task ->
        if ((task.name.contains("external") || task.name.contains("CMake")) && !task.name.contains("Clean")) {
            task.dependsOn(extractNativeLibraries)
        }
}